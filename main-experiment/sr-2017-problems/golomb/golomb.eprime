$ Golomb Ruler 
$
$ Basic Problem Model using Quaternary constraints
$ (Smith et al 'Modelling the Golomb Ruler Problem')
$
$ by Andrea Rendl
$ revised by Peter Nightingale

language ESSENCE' 1.0

given ticks : int
letting TICKS  be domain int(1..ticks)

find ruler : matrix indexed by [TICKS] of int(0..ticks**2)

minimising ruler[ticks]

such that
    
    ruler[1]=0,
    
    $ monotonicity
    forAll i : int(2..ticks) .
        ruler[i-1] < ruler[i],
    
    $ distinction 
    forAll i,j,k,l : TICKS .
        ((i < j) /\ (k < l) /\ ( [i,j] <lex [k,l] )) -> 
            (ruler[j] - ruler[i] != ruler[l] - ruler[k])


